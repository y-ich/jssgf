###
SGF libarary
(C) 2013-2014 ICHIKAWA, Yuji (New 3 Rs)
###
###
# jssgf.parse to parse SGF string
# jssgf.isSgf to check if a string is SGF
# jssgf.nthMoveNode to get a node of nth move
###

parser.stringify = (c) ->
    ###
    stringifies collection c
    parser is a export variable generated by jison
    ###
    c.map(gameTree2string).join ''

gameTree2string = (gameTree) ->
    result = '('
    n = gameTree
    loop
        result += node2string n
        if n._children.length == 0
            break
        else if n._children.length == 1
            n = n._children[0]
        else
            result += (gameTree2string e for e in n._children).join ''
            break
    result += ')'

node2string = (node) ->
    ';' + (k + propvalues2string v for k, v of node when not /^_/.test k).join ''

propvalues2string = (propvalues) ->
    propvalues = [propvalues] unless propvalues instanceof Array
    ("[#{escapePropvalue e}]" for e in propvalues).join ''

escapePropvalue = (propvalue) ->
    # propvalue.replace /([\]\\:])/g, '\\$1'
    propvalue.replace /([\]\\])/g, '\\$1' # do not escape : because it is not interpreted yet.

parser.isSgf = (sgf) ->
    ### returns true if string sgf is SGF format. ###
    try
        parser.parse sgf
        true
    catch e
        false

parser.nthMoveNode = (root, n) ->
    ###
    returns a node of primary nth move
    if n is larger than total moves, returns the last move
    ###
    num = 0
    node = root
    while node._children.length > 0 and num < n
        node = node._children[0]
        if node.B? or node.W?
            num += 1
    node

class SGFNode
    toString: ->
        node2string this

class SGFGameTree extends SGFNode
    @fromParsed: (gameTree) ->
        result = new this()
        for k, v of gameTree when k isnt '_children'
            result[k] = v
        for each in gameTree._children
            result._children.push SGFGameTree.fromParsed each
        result

    constructor: ->
        super()
        @_children = []

    toStringRecursively: ->
        result = '('
        node = this
        loop
            result += node.toString()
            if node._children.length == 0
                break
            else if node._children.length == 1
                node = node._children[0]
            else
                result += (each.toStringRecursively() for each in node._children).join ''
                break
        result += ')'

class SGFCollection extends Array
    @fromParsed: (collection) ->
        result = new this()
        for each in collection
            result.push SGFGameTree.fromParsed each
        result

    toString: ->
        (each.toStringRecursively() for each in this).join('\n')

parser.SGFCollection = SGFCollection
parser.SGFGameTree = SGFGameTree
